import QtQuick
import QtQuick.Controls
import qs.Common
import qs.Services
import qs.Widgets
import qs.Modules.Plugins

PluginComponent {
    id: root

    // Right-click action on pill
    pillRightClickAction: () => root.toggleMute()

    // Layout constants
    readonly property real cellWidth: (root.popoutWidth - (root.gridSpacing * 2) - 16) / 3
    readonly property real cellHeight: 80
    readonly property real iconSize: 28
    readonly property real fontSize: 14
    readonly property int gridSpacing: 8

    // Plugin directory (for sound files)
    readonly property string pluginDir: {
        var url = Qt.resolvedUrl(".").toString();
        if (url.startsWith("file://")) url = url.replace("file://", "");
        return url.endsWith("/") ? url.substring(0, url.length - 1) : url;
    }

    // Sound definitions
    readonly property var sounds: [
        { name: "rain", icon: "water_drop" },
        { name: "storm", icon: "thunderstorm" },
        { name: "wind", icon: "air" },
        { name: "waves", icon: "waves" },
        { name: "stream", icon: "water" },
        { name: "birds", icon: "flutter_dash" },
        { name: "summer-night", icon: "dark_mode" },
        { name: "fireplace", icon: "local_fire_department" },
        { name: "coffee-shop", icon: "local_cafe" },
        { name: "city", icon: "location_city" },
        { name: "train", icon: "train" },
        { name: "boat", icon: "sailing" },
        { name: "white-noise", icon: "blur_on" },
        { name: "pink-noise", icon: "blur_linear" }
    ]

    // Sleep timer presets
    readonly property var sleepPresets: [
        { label: "15m",  minutes: 15 },
        { label: "30m",  minutes: 30 },
        { label: "45m",  minutes: 45 },
        { label: "1h",   minutes: 60 },
        { label: "1.5h", minutes: 90 },
        { label: "2h",   minutes: 120 }
    ]

    // Audio state
    property var playingSounds: []
    property int masterVolume: pluginData.defaultVolume !== undefined ? parseInt(pluginData.defaultVolume) : 75
    property bool isMuted: false

    // Preset state
    property var presets: pluginData.presets || []
    property int editingIndex: -1

    function savePreset() {
        if (playingSounds.length === 0) {
            ToastService.showError("Play some sounds first to save as preset!");
            return;
        }
        var newPresets = presets.slice();
        var presetName = "Preset " + (newPresets.length + 1);
        newPresets.push({
            name: presetName,
            sounds: playingSounds.slice(),
            volume: root.masterVolume
        });
        presets = newPresets;
        pluginData.presets = newPresets;
        ToastService.showInfo("Saved " + presetName);
    }

    function loadPreset(preset) {
        // First kill everything and wait for it to finish
        Proc.runCommand("kill-for-preset", ["bash", "-c", killSoundCmd(".*")], (output, exitCode) => {
            root.isMuted = false;
            root.masterVolume = preset.volume;
            root.playingSounds = preset.sounds.slice();
            
            // Now start playing each sound in the preset
            for (var i = 0; i < root.playingSounds.length; i++) {
                Proc.runCommand("play-" + root.playingSounds[i], ["bash", "-c", playSoundCmd(root.playingSounds[i])], null, 0);
            }
        }, 0);
    }

    function deletePreset(index) {
        var newPresets = presets.slice();
        newPresets.splice(index, 1);
        presets = newPresets;
        pluginData.presets = newPresets;
    }

    function renamePreset(index, newName) {
        if (newName && newName.trim() !== "") {
            var newPresets = presets.slice();
            newPresets[index].name = newName.trim();
            presets = newPresets;
            pluginData.presets = newPresets;
        }
        editingIndex = -1;
    }

    // Helper – mpv commands
    function playSoundCmd(sound) {
        var vol = root.isMuted ? 0 : root.masterVolume;
        var soundFile = pluginDir + "/sounds/" + sound + ".ogg";
        return "mpv --no-video --no-config --loop=inf --volume=" + vol + " '" + soundFile + "' > /dev/null 2>&1";
    }

    function killSoundCmd(pattern) {
        return "pkill -f 'ambientSound/sounds/" + pattern + ".ogg'";
    }

    // Audio logic
    function toggleMute() {
        if (playingSounds.length === 0) return;
        isMuted = !isMuted;
        restartAll();
    }
    function toggleSound(sound) {
        var idx = playingSounds.indexOf(sound);
        var list = playingSounds.slice();

        if (idx >= 0) {
            list.splice(idx, 1);
            playingSounds = list;
            Proc.runCommand("stop-" + sound, ["bash", "-c", killSoundCmd(sound)], null, 0);
            if (list.length === 0) {
                root.isMuted = false;
            }
        } else {
            list.push(sound);
            playingSounds = list;
            Proc.runCommand("play-" + sound, ["bash", "-c", playSoundCmd(sound)], null, 0);
        }
    }

    function stopAll() {
        playingSounds = [];
        isMuted = false;
        Proc.runCommand("stop-all", ["bash", "-c", killSoundCmd(".*")], null, 0);
    }

    function restartAll() {
        var toPlay = playingSounds.slice();
        Proc.runCommand("kill-all", ["bash", "-c", killSoundCmd(".*")], (output, exitCode) => {
            for (var i = 0; i < toPlay.length; i++) {
                Proc.runCommand("play-" + toPlay[i], ["bash", "-c", playSoundCmd(toPlay[i])], null, 0);
            }
        }, 0);
    }

    function adjustVolume(delta) {
        var newVol = Math.min(100, Math.max(0, root.masterVolume + delta));
        if (newVol !== root.masterVolume) {
            root.masterVolume = newVol;
            volumeDebounceTimer.restart();
        }
    }

    // Auto‑start key generator
    function autoStartKey(soundName) {
        return "autoStart" + soundName.charAt(0).toUpperCase() + soundName.slice(1).replace("-", "");
    }

    // Timers
    Timer {
        id: volumeDebounceTimer
        interval: 250
        repeat: false
        onTriggered: root.restartAll()
    }

    Timer {
        id: autoStartTimer
        interval: 2000
        onTriggered: {
            for (var i = 0; i < root.sounds.length; i++) {
                var key = autoStartKey(root.sounds[i].name);
                if (pluginData[key]) root.toggleSound(root.sounds[i].name);
            }
            if (pluginData.enableSleepTimer) {
                var minutes = parseInt(pluginData.sleepTimerDuration) || 0;
                if (minutes > 0) {
                    sleepTimer.interval = minutes * 60 * 1000;
                    sleepTimer.remainingTime = minutes * 60 * 1000;
                    sleepTimer.start();
                }
            }
        }
    }

    Timer {
        id: sleepTimer
        property int remainingTime: 0
        onTriggered: {
            root.stopAll();
            remainingTime = 0;
        }
    }

    Timer {
        id: sleepCountdown
        interval: 1000
        repeat: true                     // tick every second
        running: sleepTimer.running
        onTriggered: sleepTimer.remainingTime = Math.max(0, sleepTimer.remainingTime - 1000);
    }

    Component.onCompleted: {
        autoStartTimer.start();
        // Initialize default preset only once
        if (pluginData.hasInitializedPresets === undefined) {
            var defaultPresets = [
                {
                    name: "Relaxing Rain",
                    sounds: ["rain", "birds", "wind"],
                    volume: 75
                }
            ];
            pluginData.presets = defaultPresets;
            presets = defaultPresets;
            pluginData.hasInitializedPresets = true;
        }
    }

    // ── Pill (horizontal & vertical) ──
    horizontalBarPill: Component {
        Item {
            implicitWidth: pillRow.implicitWidth
            implicitHeight: pillRow.implicitHeight

            // Mouse area for left click and wheel
            MouseArea {
                anchors.fill: parent
                acceptedButtons: Qt.LeftButton
                cursorShape: Qt.PointingHandCursor
                onClicked: root.triggerPopout()
                onWheel: (wheel) => {
                    var delta = wheel.angleDelta.y > 0 ? 10 : -10;
                    root.adjustVolume(delta);
                }
            }

            // Centered content row
            Row {
                id: pillRow
                anchors.centerIn: parent
                spacing: 4

                // Only show the note icon when nothing is playing or when muted
                DankIcon {
                    name: root.isMuted ? "volume_off" : "music_note"
                    size: Theme.iconSizeMedium
                    color: root.isMuted ? Theme.error : Theme.surfaceVariantText
                    visible: root.playingSounds.length === 0 || root.isMuted
                    anchors.verticalCenter: parent.verticalCenter
                }

                // Dancing bars, visible only when something is playing and NOT muted
                Row {
                    anchors.verticalCenter: parent.verticalCenter
                    spacing: 1   // tighter bars
                    visible: root.playingSounds.length > 0 && !root.isMuted
                    Repeater {
                        model: 5   // 5 bars
                        Rectangle {
                            width: 2
                            height: 6   // base height taller
                            radius: 1
                            color: Theme.primary
                            anchors.verticalCenter: parent.verticalCenter
                            Timer {
                                running: root.playingSounds.length > 0 && !root.isMuted
                                repeat: true
                                interval: 150 + (index * 30)   // varied phases
                                onTriggered: parent.height = 6 + Math.random() * 12   // up to 18px
                            }
                            Behavior on height { NumberAnimation { duration: 150 } }
                        }
                    }
                }
            }
        }
    }

    verticalBarPill: horizontalBarPill

    // Popout dimensions
    popoutWidth: 380
    popoutHeight: root.presets.length > 0 ? 550 : 470

    // Popout content
    popoutContent: Component {
        PopoutComponent {
            width: root.popoutWidth
            headerText: "Ambient Sounds"
            detailsText: root.playingSounds.length > 0 ? root.playingSounds.length + " playing" : "Tap to play"
            showCloseButton: false

            Column {
                width: parent.width
                spacing: 8

                // Volume slider
                Row {
                    width: parent.width
                    spacing: Theme.spacingS
                    DankIcon {
                        name: root.isMuted ? "volume_off" : "volume_up"
                        size: 22
                        color: root.isMuted ? Theme.error : (root.playingSounds.length > 0 ? Theme.primary : Theme.surfaceVariantText)
                        anchors.verticalCenter: parent.verticalCenter
                        MouseArea {
                            anchors.fill: parent
                            cursorShape: Qt.PointingHandCursor
                            onClicked: root.toggleMute()
                        }
                    }
                    DankSlider {
                        id: volumeSlider
                        value: root.masterVolume
                        width: parent.width - 40
                        minimum: 0; maximum: 100
                        centerMinimum: false; unit: "%"; showValue: true
                        wheelEnabled: false
                        onSliderValueChanged: v => {
                            root.masterVolume = v;
                            if (v > 0 && root.isMuted) root.isMuted = false;
                            volumeDebounceTimer.restart();
                        }
                        MouseArea {
                            anchors.fill: parent
                            acceptedButtons: Qt.NoButton // Để click vẫn xuyên qua được Slider
                            onWheel: (wheel) => {
                                var delta = wheel.angleDelta.y > 0 ? 5 : -5;
                                var newVol = Math.min(100, Math.max(0, root.masterVolume + delta));
                                if (newVol !== root.masterVolume) {
                                    root.masterVolume = newVol;
                                    if (newVol > 0 && root.isMuted) root.isMuted = false;
                                    volumeDebounceTimer.restart();
                                }
                            }
                        }
                    }
                }

                // Sound grid
                Flow {
                    width: parent.width
                    spacing: root.gridSpacing
                    
                    // 14 Sound Tiles
                    Repeater {
                        model: root.sounds
                        delegate: Rectangle {
                            width: root.cellWidth
                            height: root.cellHeight
                            radius: Theme.cornerRadius
                            color: root.playingSounds.indexOf(modelData.name) >= 0 ? Theme.primary : Theme.surfaceContainerHigh

                            Column {
                                anchors.centerIn: parent
                                spacing: 2
                                DankIcon {
                                    name: modelData.icon
                                    size: root.iconSize
                                    color: root.playingSounds.indexOf(modelData.name) >= 0 ? Theme.onPrimary : Theme.surfaceText
                                    anchors.horizontalCenter: parent.horizontalCenter
                                }
                                StyledText {
                                    text: modelData.name.replace("-", " ")
                                    font.pixelSize: root.fontSize
                                    font.weight: Font.Medium
                                    color: root.playingSounds.indexOf(modelData.name) >= 0 ? Theme.onPrimary : Theme.surfaceText
                                    anchors.horizontalCenter: parent.horizontalCenter
                                }
                            }
                            MouseArea {
                                anchors.fill: parent
                                cursorShape: Qt.PointingHandCursor
                                onClicked: root.toggleSound(modelData.name)
                            }
                        }
                    }

                    // 15th Slot: Save Preset Button
                    Rectangle {
                        width: root.cellWidth
                        height: root.cellHeight
                        radius: Theme.cornerRadius
                        color: Theme.surfaceContainerHigh
                        border.width: 1
                        border.color: Theme.surfaceVariant

                        Column {
                            anchors.centerIn: parent
                            spacing: 2
                            DankIcon {
                                name: "bookmark_add"
                                size: root.iconSize
                                color: Theme.primary
                                anchors.horizontalCenter: parent.horizontalCenter
                            }
                            StyledText {
                                text: "Save Preset"
                                font.pixelSize: root.fontSize
                                font.weight: Font.Medium
                                color: Theme.primary
                                anchors.horizontalCenter: parent.horizontalCenter
                            }
                        }
                        MouseArea {
                            anchors.fill: parent
                            cursorShape: Qt.PointingHandCursor
                            onClicked: root.savePreset()
                        }
                    }
                }

                // Footer (sleep timer + stop all)
                Column {
                    width: parent.width
                    spacing: 4

                    Row {
                        width: parent.width
                        spacing: 4
                        visible: pluginData.enableSleepTimer ?? true

                        DankIcon {
                            name: "timer"
                            size: 18
                            color: sleepTimer.running ? Theme.primary : Theme.surfaceVariantText
                            anchors.verticalCenter: parent.verticalCenter
                        }
                        StyledText {
                            text: sleepTimer.running ? Math.ceil(sleepTimer.remainingTime / 60000) + " min left" : ""
                            font.pixelSize: Theme.fontSizeSmall
                            color: sleepTimer.running ? Theme.primary : Theme.surfaceVariantText
                            anchors.verticalCenter: parent.verticalCenter
                        }

                        Repeater {
                            model: root.sleepPresets
                            delegate: DankButton {
                                text: modelData.label
                                width: 48; height: 24
                                visible: !sleepTimer.running
                                onClicked: {
                                    var ms = modelData.minutes * 60 * 1000;
                                    sleepTimer.interval = ms;
                                    sleepTimer.remainingTime = ms;
                                    sleepTimer.start();
                                }
                            }
                        }

                        DankButton {
                            text: "Off"
                            width: 48; height: 24
                            backgroundColor: Theme.errorContainer
                            textColor: Theme.error
                            visible: sleepTimer.running
                            onClicked: sleepTimer.stop()
                        }
                    }

                    DankButton {
                        text: "Stop All"
                        iconName: "stop"
                        width: parent.width
                        visible: root.playingSounds.length > 0
                        backgroundColor: Theme.errorContainer
                        textColor: Theme.error
                        onClicked: root.stopAll()
                    }

                    // Presets area
                    Column {
                        width: parent.width
                        spacing: 4
                        visible: root.presets.length > 0

                        StyledText {
                            text: "Your Presets"
                            font.pixelSize: Theme.fontSizeSmall
                            font.weight: Font.Bold
                            color: Theme.surfaceVariantText
                        }

                        Flow {
                            width: parent.width
                            spacing: 4
                            Repeater {
                                model: root.presets
                                delegate: Item {
                                    width: (parent.width - 4) / 2
                                    height: 32

                                    DankButton {
                                        id: presetButton
                                        text: modelData.name
                                        width: parent.width - 48
                                        height: parent.height
                                        visible: root.editingIndex !== index
                                        onClicked: root.loadPreset(modelData)
                                    }

                                    DankTextField {
                                        id: editField
                                        width: parent.width - 48
                                        height: parent.height
                                        text: modelData.name
                                        visible: root.editingIndex === index
                                        onEditingFinished: root.renamePreset(index, text)
                                        Component.onCompleted: {
                                            if (root.editingIndex === index) forceActiveFocus();
                                        }
                                    }

                                    DankIcon {
                                        name: root.editingIndex === index ? "check" : "edit"
                                        size: 16
                                        anchors.right: deleteIcon.left
                                        anchors.rightMargin: 4
                                        anchors.verticalCenter: parent.verticalCenter
                                        color: Theme.surfaceVariantText
                                        MouseArea {
                                            anchors.fill: parent
                                            cursorShape: Qt.PointingHandCursor
                                            onClicked: {
                                                if (root.editingIndex === index) {
                                                    root.renamePreset(index, editField.text);
                                                } else {
                                                    root.editingIndex = index;
                                                }
                                            }
                                        }
                                    }
                                    
                                    DankIcon {
                                        id: deleteIcon
                                        name: "close"
                                        size: 16
                                        anchors.right: parent.right
                                        anchors.verticalCenter: parent.verticalCenter
                                        color: Theme.error
                                        MouseArea {
                                            anchors.fill: parent
                                            cursorShape: Qt.PointingHandCursor
                                            onClicked: root.deletePreset(index)
                                        }
                                    }
                                }
                            }
                        }
                    }

                    StyledText {
                        text: "Right-click icon to mute/unmute"
                        font.pixelSize: Theme.fontSizeSmall
                        color: Theme.surfaceVariantText
                        anchors.horizontalCenter: parent.horizontalCenter
                        visible: root.playingSounds.length > 0 && (pluginData.showReminderText ?? true)
                    }
                }
            }
        }
    }
}